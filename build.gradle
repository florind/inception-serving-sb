buildscript {
	ext {
		springBootVersion = '2.7.4'
	}
	repositories {
		mavenCentral()
		maven { url "https://repo.spring.io/snapshot" }
		maven { url "https://repo.spring.io/milestone" }
	}
	dependencies {
		classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
		classpath "de.undercouch:gradle-download-task:5.2.1"
		classpath "net.saliman:gradle-cobertura-plugin:4.0.0"
		classpath "com.google.guava:guava:31.1-jre"
	}
}

plugins {
	id 'org.asciidoctor.jvm.convert' version '3.1.0'
	id 'com.github.kt3k.coveralls' version '2.12.0'
	id 'io.franzbecker.gradle-lombok' version '5.0.0'
}

apply plugin: 'java'
apply plugin: 'org.springframework.boot'
apply plugin: 'io.spring.dependency-management'
apply plugin: 'application'
apply plugin: 'de.undercouch.download'
apply plugin: 'net.saliman.cobertura'

configurations {
	asciidoctorExt
}

sourceCompatibility = 8

repositories {
	mavenCentral()
	maven { url "https://repo.spring.io/snapshot" }
	maven { url "https://repo.spring.io/milestone" }
}

dependencies {
  implementation('org.springframework.boot:spring-boot-starter-web')
  implementation('org.projectlombok:lombok:1.18.24')
  implementation('org.tensorflow:tensorflow:1.15.0')
  implementation('commons-io:commons-io:2.11.0')
  implementation('jmimemagic:jmimemagic:0.1.2')

  asciidoctorExt('org.springframework.restdocs:spring-restdocs-asciidoctor:2.0.6-RELEASE')
  testImplementation('org.springframework.restdocs:spring-restdocs-mockmvc')
  testImplementation('org.springframework.boot:spring-boot-starter-test')
}

ext {
  snippetsDir = file('build/generated-snippets')
}

asciidoctor {
	inputs.dir snippetsDir
	dependsOn test
}

test {
  outputs.dir snippetsDir
}

task copyRestDocs(type: Copy) {
  dependsOn asciidoctor
  from "${asciidoctor.outputDir}/html5"
  into "${sourceSets.main.output.resourcesDir}/static/docs"
}

jar {
	baseName = "inception-serving-sb"
	dependsOn copyRestDocs
	from("build/asciidoc/html5") {
		into 'public/docs'
	}
}

bootRun {
  dependsOn copyRestDocs
  //http://stackoverflow.com/a/31328621/4985623
  systemProperties System.properties
}

mainClassName = 'com.newsplore.Application'

import de.undercouch.gradle.tasks.download.Download

task downloadInceptionFrozen(type: Download) {
  onlyIfNewer true
  src 'https://storage.googleapis.com/download.tensorflow.org/models/inception_v3_2016_08_28_frozen.pb.tar.gz'
  dest new File(buildDir, 'inception_v3_2016_08_28_frozen.pb.tar.gz')
}

task fetchInceptionFrozenModel(dependsOn: downloadInceptionFrozen, type: Copy) {
  from tarTree(downloadInceptionFrozen.dest)
  into 'src/main/resources/inception-v3'
}


lombok {
	version = '1.18.24'
	sha256 = ""
}

build.dependsOn fetchInceptionFrozenModel, copyRestDocs
cobertura.coverageFormats = ['html', 'xml']
